/*------------------------------------------------------------------------------

    TEMPLATE FILE FOR DEFINING RETICULAR THALAMIC NEURONS
    -----------------------------------------------------

    One compartment model and currents derived from:

    Destexhe, A., Contreras, D., Sejnowski, T.J. and Steriade, M.
    A model of spindle rhythmicity in the isolated thalamic reticular
    nucleus.  J. Neurophysiol. 72: 803-818, 1994.

    Destexhe, A., Contreras, D., Steriade, M., Sejnowski, T.J.,
    and Huguenard, J.R.  In vivo, in vitro and computational analysis of 
    dendritic calcium currents in thalamic reticular neurons.  
    Journal of Neuroscience 16: 169-185, 1996.

    Modifications:

    - passive parameters estimated from simplex
    - IT2: Q10=2.5, strong conductance for broad bursts
    - no IK[Ca], no ICAN
    - Ca++: simple decay only



    This model is described in detail in:

    Destexhe, A., Bal, T., McCormick, D.A. and Sejnowski, T.J.
    Ionic mechanisms underlying synchronized oscillations and propagating
    waves in a model of ferret thalamic slices. Journal of Neurophysiology
    76: 2049-2070, 1996.  (see http://www.cnl.salk.edu/~alain)


    Alain Destexhe, Salk Institute and Laval University, 1995

--------------------------------------------------------------------------------

    Requires:
        cd/HH2.mod
        cd/ITs.mod
        cd/IKCa.mod
        cd/cadecay.mod
        cd/cldecay.mod
        cd/cldecay1.mod
        cd/cldecay2.mod
    Used by:    
        cd/net.hoc

    File history:
    2017-02-07 Modified from RE.tem of Dextexhe et al 1996 using parameters 
                from Sohal & Huguenard 2003
    2017-02-22 Inserted cldifus2
    2017-03-05 leak is no longer a parameter for cldifus2
    2017-03-05 clo0_cl_ion changed from 1 (default) to 132.5
    2017-03-07 Divided soma into three compartments so that icl can be 
                concentrated in the center compartment
    2017-03-14 Made REuseca, REcldnum, REtauKCC2, REepas arguments for init()
    2017-03-15 vmax is now calculated based on the parameters 
                tauKCC2, clinf & Kd
    2017-03-21 Added REnsegs
    2017-03-22 Added REconsyn
    2017-03-28 Make REdiam an argument and force L_total to be 
                twice the diameter
    2017-03-31 REuseca is changed to useca
    2017-03-31 Changed the way useca is implemented: 
                still inserts the channels but makes permeabilities zero
    2018-05-09 Reorganized comments

------------------------------------------------------------------------------*/

begintemplate REcell        // create template for reticular thalamic neurons

/* Declare public variables */
public soma, soma_flank

/* Create compartments (sections) */
create soma[1]
create soma_flank[2]

/* Set up geometry and insert currents */
proc init() { local L_total
// $1 = number of segments in an RE cell (REnsegs)
// $2 = whether to use ca_ion (0 or 1) in RE cells (REuseca)
// $3 = which cld mechanism to use (0 or 1 or 2) (REcldnum)
// $4 = whether to concentrate synapses (0 or 1) in RE cells (REconsyn)
// $5 = Cl- removal time constant (s) (REtauKCC2)
// $6 = leak reversal potential (mV) (REepas)
// $7 = diameter of an RE cell (REdiam)

    L_total = 2*$7            // length (um) of soma, based on neuron in Yen et al 1985 (p. 1252 of Thalamus book)

    /* Soma: total length = 70 um; total surface area = 15393 um2 = 0.000154 cm2 */
    soma {
        if ($4) {
            L = 0.26    // length (um) of center compartment
                    // based on the length of synaptic contact seen on EM, Hallanger & Wainer 1988
                    // the maximum length of synaptic contact zone in Cucchiaro et al 1991 is 0.5 um
            nseg = 1    // number of segments for the soma is always 1 in this case
        } else {
            L = L_total    // only one soma
            nseg = $1    // number of segments of the soma is the same for the entire cell
        }
    }

    if ($4) {
        for j=0,1 {
            soma_flank[j] {
                L = (L_total-soma.L)/2    // length (um) of left compartment
                if ($1 >= 3) {
                    nseg = ($1-1)/2        // number of segments
                } else {
                    nseg = 1
                }
            }
        }

        /* Connect compartments */
        soma connect soma_flank[0](0), 0
        soma connect soma_flank[1](0), 1
    }

    /* Define other geometry and insert mechanisms for all sections */
    forall {
        /* Define geometry */
        diam = $7        // diameter (um), based on neuron in Yen et al 1985 (p. 1252 of Thalamus book)

        /* Set passive properties */
        cm = 1            // specific capacitance (uF/cm2)
        Ra = 100        // axial resistivity (Ohm-cm), Destexhe's value 
                    //     Jedlicka et al 2011 used 150 Ohm-cm, but it doesn't seem to matter

        /* Insert leak current */
        insert pas        
        e_pas = $6        // leak reversal potential (mV) of RE cells, Peter's value
                    // Sohal & Huguenard 2003 used -77 mV
                    // Jedlicka et al 2011 used -60 mV
        g_pas = 5e-5        // leak conductance (S/cm^2)
                    // In Sohal & Huguenard 2003, this is uniformly randomized from (4.5~5.5)e-5

        /* Insert Hodgin-Huxley Na+ and K+ current */
        insert hh2        // READ ena, ek WRITE ina, ik
        ena = 50        // Na+ reversal potential (mV), Sohal & Huguenard 2003
        ek = -104        // K+ reversal potential (mV), Peter's value 
                    //     (based on [K]out = 2.5 mM, [K]in = 124 mM & celsius = 31 degC)
        vtraub_hh2 = -47    // action potential threshold (mV), TODO: Source (Destexhe used -55 mV)
        gnabar_hh2 = 0.05    // Na+ conductance (S/cm2), Sohal & Huguenard 2003
        gkbar_hh2 = 0.005    // K+ conductance (S/cm2), Sohal & Huguenard 2003

        /* Insert low-threshold Ca++ current */
        insert Ts        // READ cai, cao WRITE ica
        if ($2) {
            pcabar_Ts = 1e-4    // Ca++ permeability (cm/s) for T channels
        } else {
            pcabar_Ts = 0        // Ca++ permeability (cm/s) for T channels
        }

        /* Insert Ca-dependent K+ current */
        insert kca        // READ cai, ek WRITE ik
        if ($2) {
            gbar_kca = 1.5e-3    // [Ca++]i-dependent K+ conductance (mho/cm2 mM), Sohal & Huguenard 2003
        } else {
            gbar_kca = 0        // [Ca++]i-dependent K+ conductance (mho/cm2 mM), Sohal & Huguenard 2003
        }

        /* Insert submembranal Ca++ pump */
        insert cad        // READ ica, cai WRITE cai
        depth_cad = 0.1        // depth of shell for Ca++ (um)
        taur_cad = 24        // Ca++ removal time constant (ms), Sohal & Huguenard 2003
        cainf_cad = 2.4e-4    // steady state intracellular [Ca++] (mM)

        /* Insert submembranal Cl- concentration */
        if ($3 == 0) {
            insert cld        // READ cli, icl WRITE cli
            depth_cld = 0.1        // depth of shell for Cl- (um)
            tauKCC2_cld = $5    // Cl- removal time constant (ms), Peter's value 
                        // (Jedlicka et al 2011 used 3000 ms)
            clinf_cld = 8        // steady state intracellular [Cl-] (mM)
        } else if ($3 == 1) {
            insert cld1        // READ cli, icl WRITE cli
            depth_cld1 = 0.1    // relative depth of shell (to diameter) for Cl-,
                        //     this gives 0.5 um for a cell of diameter 5 um
            DCl_cld1 = 2        // Cl- diffusion coefficient (um2/ms), Brumback & Staley 2008
                        // also Kuner & Augustine 2000, Neuron 27: 447
            tauKCC2_cld1 = $5    // Cl- removal time constant (ms), Peter's value 
                        // (Jedlicka et al 2011 used 3000 ms)
            clinf_cld1 = 8        // steady state intracellular [Cl-] (mM)
        } else if ($3 == 2) {
            insert cld2        // READ cli, icl WRITE cli
            depth_cld2 = 0.1    // relative depth of shell (to diameter) for Cl-
                        //     this gives 0.5 um for a cell of diameter 5 um
            DCl_cld2 = 2        // Cl- diffusion coefficient (um2/ms), Brumback & Staley 2008
                        // also Kuner & Augustine 2000, Neuron 27: 447
            tauKCC2_cld2 = $5    // Cl- removal time constant (ms), Peter's value 
                        // (Jedlicka et al 2011 used 3000 ms)
            clinf_cld2 = 8        // steady state intracellular [Cl-] (mM)
            Kd_cld2 = 15        // [Cl-] for half-maximum flux for KCC2 (mM), Staley & Proctor 1999
        }
    }

}
endtemplate REcell

/*
OLD CODE:

*/


